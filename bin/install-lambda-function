#!/usr/bin/env perl

use strict;
use warnings;

use AWS::CLIWrapper;
use AWS::Lambda::Toolkit::Config;
use JSON::MaybeXS qw/encode_json/;

my $config = AWS::Lambda::Toolkit::Config->new;


my $cli = AWS::CLIWrapper->new(
    region => $config->region,
);

my $ret = $cli->lambda('get-function', { 
  'function-name' => $config->lambda_name,
});

sub env_to_cli_param {
  my $env = shift;
  return encode_json({ Variables => $env });
}

my $role = $config->role or die "Please configure a role in the config file";
my $handler = $config->handler or die "Please configure a handler in the config file";

if (not defined $ret) {
  my $creation = $cli->lambda('create-function', {
    'function-name' => $config->lambda_name,
    'runtime' => 'provided',
    'role' => $role,
    'handler' => $handler,
    'zip-file' => 'fileb://' . $config->function_layer_file,
    'layers' => [
      $config->perl_runtime_layer,
      $config->deps_layer,
    ],
    'environment' => env_to_cli_param($config->env),
    'timeout' => $config->timeout,
    'memory-size' => $config->memory,
  });
} else {
  my $update_code = $cli->lambda('update-function-code', {
    'function-name' => $config->lambda_name,
    'zip-file' => 'fileb://' . $config->function_layer_file,
  });

  my $update_layers = $cli->lambda('update-function-configuration', {
    'role' => $role,
    'handler' => $handler,
    'function-name' => $config->lambda_name,
    'layers' => [
      $config->perl_runtime_layer,
      $config->deps_layer,
    ],
    'environment' => env_to_cli_param($config->env),
    'timeout' => $config->timeout,
    'memory-size' => $config->memory,
  });
}

